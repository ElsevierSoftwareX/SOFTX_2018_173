apply plugin: 'application'

mainClassName = 'de.fhg.igd.georocket.GeoRocketCli'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "io.vertx:vertx-rx-java:$vertxVersion"
    
    compile 'commons-io:commons-io:2.4'
    compile 'de.undercouch:citeclip:1.0.0-SNAPSHOT'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'net.time4j:time4j-core:4.7'
}

task generateVersionFile << {
    def dstRes = new File(project.projectDir, 'src-gen/main/resources')
    def dst = new File(dstRes, 'de/fhg/igd/georocket')
    dst.mkdirs()
    def versionFile = new File(dst, 'version.dat')
    versionFile.withWriter { w ->
        w << project.version
    }
}

task generateSources(dependsOn: generateVersionFile)

processResources.dependsOn(generateSources)
eclipseClasspath.dependsOn(generateSources)

task cleanGeneratedSources(type: Delete) {
    delete 'src-gen'
}

clean.dependsOn(cleanGeneratedSources)

// include generated sources
sourceSets.main.java.srcDirs 'src-gen/main/java'
sourceSets.main.resources.srcDirs 'src-gen/main/resources'

